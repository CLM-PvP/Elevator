plugins {
    id 'java'
    id 'io.papermc.paperweight.userdev' version '1.7.2'
    id 'com.github.johnrengelman.shadow' version '7.1.0'
}

compileJava {
    options.encoding = 'UTF-8'
}

group 'org.sunlightdev'
version getBuildCount()

repositories {
    mavenCentral()
    maven {
        name = "papermc-repo"
        url = "https://repo.papermc.io/repository/maven-public/"
    }
}

dependencies {
    paperweight.paperDevBundle("1.21.1-R0.1-SNAPSHOT")
    compileOnly "io.papermc.paper:paper-api:1.21.1-R0.1-SNAPSHOT"
    implementation files('API/EssentialsX.jar')
}

paperweight.reobfArtifactConfiguration = io.papermc.paperweight.userdev.ReobfArtifactConfiguration.Companion.MOJANG_PRODUCTION

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

jar {
    duplicatesStrategy = DuplicatesStrategy.INCLUDE
    from {
        subprojects.collect { it.sourceSets.main.output }
    }
    from(files("build-count.properties"))
    from(files("hash.txt"))

}

test {
    useJUnitPlatform()
}

def getBuildCount() {
    def buildCount = 0

    if (file('build-count.properties').exists()) {
        def properties = new Properties()
        file('build-count.properties').withInputStream { properties.load(it) }
        buildCount = properties.getProperty('count')?.toInteger() ?: 0
    }

    buildCount++

    def properties = new Properties()
    properties.setProperty('count', buildCount.toString())
    file('build-count.properties').withOutputStream { properties.store(it, null) }

    buildCount
}